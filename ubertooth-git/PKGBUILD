# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ubertooth-git'
pkgver=r1114.6639145
pkgrel=1
pkgdesc='A 2.4 GHz wireless development board suitable for Bluetooth experimentation. Open source hardware and software. Tools only.'
url='https://github.com/greatscottgadgets/ubertooth/releases'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('libusbx' 'libbtbb' 'bluez-libs' 'libpcap' 'python2' 'python2-numpy'
         'python2-pyside' 'python2-pyqt4' 'python2-pyusb')
makedepends=('cmake')
source=("ubertooth::git+https://github.com/greatscottgadgets/ubertooth.git" "arch.patch")
md5sums=('SKIP'
         '75e186c1f28fc2c710fc832f7a3ccae6')

pkgver() {
	cd "$srcdir/${pkgname%-git}"

# Git, no tags available
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"

  find "$srcdir" -name "*.py" -exec sed -i 's|env python|env python2|' {} \;
  find "$srcdir" -name "*.py" -exec \
    sed -i 's|/usr/bin/python|/usr/bin/python2|' {} \;
  find "$srcdir" -name "ubertooth-specan-ui" -exec sed -i 's|env python|env python2|' {} \;

  patch -p1 -i ../arch.patch
}

build() {
  cd "$srcdir/${pkgname%-git}/host"

  mkdir build

  cd build

  cmake -DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2.7 \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr  ..

  make
}

package() {
  cd "$srcdir/${pkgname%-git}/host"

  cd build

  make DESTDIR="$pkgdir" install
}
